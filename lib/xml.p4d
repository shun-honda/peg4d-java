// PEG4d file
// Nested CDATA Section

export
        = Xml
File
	= PROLOG? S* DTD? S* Xml S*
Chunk
	= Xml

PROLOG
	= '<?xml' (!'?>' .)* '?>'

DTD
	= '<!' (!'>' .)* '>'

Xml
	[example: <x><y z="z"/></x>]
	= { '<' @Name (S+ @Attribute)* S* ( '/>' / '>' S* (@Content / COMMENT)* '</' NAME '>' ) #Element } S*

Name
	= { NAME #Name }

NAME
	= [A-Za-z:] ('-' / [.0-9:A-Z_a-z])*

Attribute
	= { @Name S* '=' S* @String #Attr }

String
	= '"' { (!'"' .)* #Value } '"'

Content
	= Xml 
	/ CDataSec 
	/ Text

CDataSec
	[example: <![CDATA[ .. <![CDATA[ .. ]]> .. ]]>]
	= '<![CDATA[' { CDATA #CDATA } ']]>' S*
CDATA
	= (!']]>' !'<![CDATA[' .)*
	  ('<![CDATA[' CDATA ']]>' CDATA)?

COMMENT
	= '<!--' (!'-->' .)* '-->' S*

Text
	= { (!'<' .)+ #Text }

