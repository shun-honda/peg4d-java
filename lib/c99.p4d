// PEG4d C2011 Grammar
// ===============
//
// Based on grammar from ISO/IEC 9899:2011 [1]. 
//
// Limitations:
//
//   * Non-BMP characters are completely ignored to avoid surrogate pair
//     handling.
//
// Many thanks to inimino [3] for his grammar [4] which helped me to solve some
// problems (such as automatic semicolon insertion) and also served to double
// check that I converted the original grammar correctly.
//
// [1] ISO/IEC 9899:2011

export
	= Statement
	/ Expression
	/ CInteger
	/ CFloat
	/ CString
	/ CChar

File
	= _ {
			( @TopLevel
			/ @{ @StructDecl @Semicolon #StructDeclaration }
			/ @Statement
			/ @Expression
			/ @Type
			/ _ )*
		#Source } EOT

AfterSemicolon
	= TopLevel
	/ StructDecl Semicolon
	/ "}"
	/ Statement
	/ Expression
	/ Type
	/ _

Chunk
	= TopLevel
	/ Statement
	/ Expression
	/ Type
_
	= (S / BLOCKCOMMENT / LINECOMMENT )*

S
	= [ \n\r\t\u000B\u000C]

BLOCKCOMMENT
	= '/*' (!'*/' .)* '*/'

LINECOMMENT
	= '//' (!'\n' .)*

//PRAGMA
//	= '#' (!'\n' .)*

ATTRIBUTE
	= '__attribute__' !W _ '((' (!'))' .)* '))' _

EOL
	= ('\r'? '\n' / !.)

DIGIT
	= [0-9]

W
	= [a-zA-Z0-9_]
	/ UCHAR

UCHAR
	= '\\u' HEX4
	/ '\\U' HEX4 HEX4

HEX4
	= HEX HEX HEX HEX

Name
	= { NAME #Name } _ 

NAME
	= !DIGIT !(KEYWORD !W) W+

KEYWORD
	= 'auto' / 'break' / 'case' / 'char' / 'const' / 'continue' / 'default' 
	/ 'double' / 'do' / 'else' / 'enum' / 'extern' / 'float' / 'for' 
	/ 'goto' / 'if' / 'int' / 'inline' / 'long' / 'register' / 'restrict' 
	/ 'return' / 'short' / 'signed' / 'sizeof' / 'static' / 'struct' 
	/ 'switch' / 'typedef' / 'union' / 'unsigned' / 'void' / 'volatile' 
	/ 'while' / '_Bool' / '_Complex' / '_Imaginary' / '_stdcall' 
	/ '__declspec' / '__attribute__' / '__asm'

"++"     = '++' _
"--"     = '--' _
"&"     = '&' !'&' _
"*"    = '*' !'=' _
"+"    = '+' ![+=] _
"-"   = '-' !('-' / [=>]) _
"~"   = '~' _
"!"    = '!' !'=' _
"void"      = 'void'     !W
"char"      = 'char'     !W
"short"     = 'short'    !W
"int"       = 'int'      !W
"long"      = 'long'     !W
"float"     = 'float'    !W
"double"    = 'double'   !W
"signed"    = 'signed'   !W _
"unsigned"  = 'unsigned' !W _
"_Bool"      = '_Bool'    !W
"_Complex"   = '_Complex' !W
"struct"    = 'struct'   !W _
"union"     = 'union'    !W _
"{"     = '{' _
"["      = '[' _
"]"      = ']' _
","	  = ',' _
"..."	= '...' _
":"	= ':' ![>] _
"/"	= '/' !'=' _
"%"	= '%' ![=>] _
"<<"	= '<<' !'=' _
">>"	= '>>' !'=' _
"<="	= '<=' _
">="	= '>=' _
"<"	= '<' ![=] _
">"	= '>' ![=] _
"=="	= '==' _
"!="	= '!=' _
"^"	= '^' ![=] _
"|"	= '|' ![=] _
"&&"	= '&&' _
"||"	= '||' _
"?"	= '?' _
";"	= (';' _ / EOL)  /* this is for debug */
"}"	= '}' _
"->"	= '->' _
"enum"	= 'enum' !W _
"__asm"	= '__asm' !W _
"case"	= 'case' !W _
"default"	= 'default' !W _
"if"	= 'if' !W _
"else"	= 'else' !W _
"switch"	= 'switch' !W _
"while"	= 'while' !W _
"do"	= 'do' !W _
"for"	= 'for' !W _
"goto"	= 'goto' !W _
"continue"	= 'continue' !W _
"break"	= 'break' !W _
"return"	= 'return' !W _
"*="	= '*=' _
"/="	= '/=' _
"%="	= '%=' _
"+="	= '+=' _
"-="	= '-=' _
"<<="	= '<<=' _
">>="	= '>>=' _
"&="	= '&=' _
"^="	= '^=' _
"|="	= '|=' _
"="	= '=' !'=' _
"."	= '.' _
"sizeof"	= 'sizeof' !W _
"typedef"	= 'typedef' !W _
"extern"	= 'extern' !W _
"static"	= 'static' !W _
"auto"	= 'auto' !W _
"register"	= 'register' !W _
"const"	= 'const' !W _
"restrict"	= 'restrict' !W _
"volatile"	= 'volatile' !W _
"__declspec"	= '__declspec' !W _
"inline"	= 'inline' !W _
"_stdcall"	= '_stdcall' !W _

"("	= '(' _
")"	= ')' _

"#"	= '#' _
"include"	= 'include' !W _
"define"	= 'define' !W _

LBR
	= "(" _
	/ {`( not found` #Message }

RBR
	= ")" _ ")" (")" / _)+ { `too many )` #Message}
	/ ")"
	/ {`) not found` #Message }

EOT	= !.

TopLevel
	= FunctionDefinition
	/ { @{ @AnnotationList @Type (@InitDeclAssign / @InitDecl ("," @InitDecl)+ )  ATTRIBUTE? @Semicolon #Declaration } #DeclarationStatement }
	/ { @[0]AnnotationList @[1]Type @[2]Name @[3]LBR @[4]FunctionParamList @[5]RBR @[6]Semicolon #Function }
	/ { @Declaration #DeclarationStatement }
	/ TypeDef
	/ Macro

Macro
	= "#" "include" { (!'\n' .)* #Include }
	/ "#" "define"  { (!'\n' .)* #Define }

FunctionDefinition
	= { @[0]AnnotationList @[1]Type @[2]Name @[3]LBR @[4]FunctionParamList @[5]RBR @[6]Block #Function }

AnnotationList
	= { (@Annotation)*  #List } _

Annotation
	= "extern"     { #KeyValue @{`extern` #Key } @{`true` #Value } } _
	/ "static"     { #KeyValue @{`static` #Key } @{`true` #Value } } _
	/ "auto"       { #KeyValue @{`auto` #Key } @{`true` #Value } } _
	/ "register"   { #KeyValue @{`register` #Key } @{`true` #Value } } _
	/ "volatile"   { #KeyValue @{`volatile` #Key } @{`true` #Value } } _
	/ "__declspec" "(" { #KeyValue @{`volatile` #Key } @(Name #Value) } _ ")" _
	/ "inline"  { #KeyValue @{`inline` #Key } @{`true` #Value } } _
	/ "_stdcall"  { #KeyValue @{`stdcall` #Key } @{`true` #Value } } _

FunctionParamList
	= { _ 'void' _ #List }
	/ { ( @FunctionParam ("," @FunctionParam)* )? ("," "...")? #List } 

FunctionParam
	= { @Type _ @VarName #Param} _
	
// Declaration
//==============

TypeDef
	= { "typedef" @Type @Name _ @Semicolon #TypeDeclaration }

Declaration
	= { @AnnotationList @Type @InitDecl ("," @InitDecl)*  ATTRIBUTE? @Semicolon #Declaration }

InitDecl
	= { @VarName ("=" @Initializer)? #VarDecl } _

InitDeclAssign
	= { @VarName "=" @Initializer #VarDecl } _

VarName
	= { "*" @VarName #PointerName } _
	/ Name {@ "[" (@ConstantExpression)? "]" #ArrayName}*

Initializer
	= AssignmentExpression
	/ "{" { (AddInitializerList)* ","? #Initializer } "}"

AddInitializerList
	= (@Designation / @Initializer)? ("," (@Designation / @Initializer))*

Designation
	= { (@Designator)+ "=" @Initializer  #Designation }
	
Designator
	= "[" ConstantExpression "]"
	/ "." Name _


//Type
//====

Type
	= { "const" _ @TypeSuffix #TConst } _
	/ TypeSuffix

TypeSuffix
	= PrimaryType {@ ( POINTER_QUALIFIER @{ "*" #TPointer } #TConst / "*" (@POINTER_QUALIFIER)* #TPointer / "[" / (NAME / DIGIT*) "]" #TPointer / AddFuncType ) }*

POINTER_QUALIFIER
	= "const" / "restrict"

AddFuncType
	= "(" "*" @Name ")" "(" (@Type (_ NAME _)? ("," @Type (_ NAME _)? )* )? ")" #TFunc

PrimaryType
	= StructDecl
	/ { "union"  @Name AddStructMember?  #TUnion } _
	/ { "enum"   @Name "{" @EnumeratorList ","? "}" #TEnum } _
	/ { "void" #TVoid } _
	/ { SIGN? "char"          #TInt } _
	/ { SIGN? "short"         #TInt } _
	/ { SIGN? "int"           #TInt } _
	/ { "float"           #TFloat } _
	/ { "double"          #TFloat } _
	/ { "long" _ "double"   #TFloat } _
	/ { SIGN? "long" (_ "long")? (_ "int")? #TInt } _
	/ { "signed" `signed int` #TInt } _
	/ { "unsigned" `unsigned int` #TInt } _
	/ { "_Bool" #TBoolean } _
	/ { "_Complex" #TComplex } _
	/ { NAME_T #TName } _
	/ { NAME   #Name } _

StructDecl
	= { "struct" @Name AddStructMember? #TStruct }

SIGN
	= ("signed" / "unsigned")

NAME_T
	= !DIGIT (!('_t' !W)  W )+ '_t' !W
	
AddStructMember
	= '{' _ (@StructDeclaration / _ )+ _ "}"

StructDeclaration
	= { @AnnotationList @Type @StructMemberName  ("," @StructMemberName )* @Semicolon _ #StructMemberDeclaration }

StructMemberName
	= VarName {@ ":" @ConstantExpression #Bit}*

EnumeratorList
	= { @Enumerator ("," @Enumerator)* ","? #List }

Enumerator
	= Name {@ "=" @ConstantExpression}?


// Block, Statement
//===================

Block
	= { "{" (@(Statement / { @Declaration #DeclarationStatement }))* "}" #Block }

Semicolon
	= ";"
	/ [:,] &_ &AfterSemicolon { `Semicolon` #Message}
	/ &_ &AfterSemicolon { `Semicolon` #Message}

Statement
	= Block
	/ { "if"     @[0]LBR @[1]Expression @[2]RBR @[3]Statement ("else" @[4]Statement)? #If }
	/ { "switch" @[0]LBR @[1]Expression @[2]RBR @[3]Statement #Switch }
	/ { "while"  @LBR @Expression @RBR @Statement #While }
	/ { "do" @[0]Statement "while" @[1]LBR @[2]Expression @[3]RBR @Semicolon #DoWhile }
	/ { "for" @[0]LBR (@[1]Expression)? @[2]Semicolon (@[3]Expression)? @[4]Semicolon (@[5]Expression)? @[6]RBR @[7]Statement #For }
	/ { "for" @[0]LBR @[1]Declaration (@[3]Expression)? @[4]Semicolon (@[5]Expression)? @[6]RBR @[7]Statement #For }
	/ { "goto" @Name @Semicolon #Goto }
	/ { "continue" @Semicolon #Continue }
	/ { "break" @Semicolon #Break }
	/ { "return" (@Expression)?  @Semicolon #Return }
	/ { "case" @ConstantExpression ":" (!"case" !"default" @Statement)* #SwitchCase }
	/ { "default" ":" (!"case" @Statement)* #SwitchDefault }
	/ { @Name ":" #Label }
	/ { @Expression @Semicolon #ExpressionStatement }
	/ { #Empty } ";"


//Expression
//==========

Expression
	= AssignmentExpression {@ "," @AssignmentExpression #Expression }*

AssignmentExpression
	= { @UnaryExpression AddAssignmentOperator @AssignmentExpression }
	/ ConditionalExpression

AddAssignmentOperator
	= "="        #Assign
	/ "*="    #AssignMul
	/ "/="     #AssignDiv
	/ "%="     #AssignMod
	/ "+="    #AssignAdd
	/ "-="   #AssignSub
	/ "<<="    #AssignLeftShift
	/ ">>="   #AssignRightShift
	/ "&="     #AssignBitwiseAnd
	/ "^="     #AssignBitwiseXOr 
	/ "|="      #AssignBitwiseOr

ConstantExpression
	= ConditionalExpression
	
ConditionalExpression
	= LogicalORExpression {@ "?"  @Expression ":" @LogicalORExpression #Trinary }*

LogicalORExpression
	= LogicalANDExpression {@ "||" @LogicalANDExpression #Or }*

LogicalANDExpression
	= InclusiveORExpression {@ "&&" @InclusiveORExpression #And }*

InclusiveORExpression
	= ExclusiveORExpression {@ "|" @ExclusiveORExpression #BitwiseOr }*

ExclusiveORExpression
	= ANDExpression {@ "^" @ANDExpression #BitwiseXor }*

ANDExpression
	= EqualityExpression {@ "&" @EqualityExpression #BitwiseAnd }*

EqualityExpression
	= RelationalExpression {@ ( "==" #Equals / "!=" #NotEquals ) @RelationalExpression }*
	
RelationalExpression
	= ShiftExpression {@ ( "<=" #LessThanEquals / ">=" #GreaterThanEquals / "<" #LessThan / ">" #GreaterThan ) @ShiftExpression }*
	
ShiftExpression
	= AdditiveExpression {@ ( "<<" #LeftShift / ">>" #RightShift ) @AdditiveExpression }*
	
AdditiveExpression
	= MultiplicativeExpression {@ ( "+" #Add / "-" #Sub ) @MultiplicativeExpression }*
	
MultiplicativeExpression
	= CastExpression {@ ( "*" #Mul / "/" #Div / "%" #Mod ) @CastExpression }*

UnaryExpression
	= PostfixExpression
	/ { "++"    @UnaryExpression #PrefixInc }
	/ { "--"    @UnaryExpression #PrefixDec }
	/ { "&"    @CastExpression #Address }
	/ { "*"   @CastExpression #Star }
	/ { "+"   @CastExpression #Plus }
	/ { "-"  @CastExpression #Minus }
	/ { "~"  @CastExpression #Compl }
	/ { "!"   @CastExpression #Not }
	/ { "sizeof"  ( @UnaryExpression / "(" @Type ")" ) #SizeOf }

CastExpression
	= { "(" @Type ")" @CastExpression #Cast }
	/ UnaryExpression

PostfixExpression
	= ( PrimaryExpression / { "(" @Type ")" "{" @{ (AddInitializerList)* ","? #Initializer } "}" } ) 
	{@ (AddFunctionCall / AddIndex  / "." @Name #Field / "->" @Name #PointerField / "++" #Inc / "--" #Dec) }*

AddFunctionCall
	= @{ "(" AddArgumentExpressionList? @RBR #List} #Apply

AddArgumentExpressionList
	= (@AssignmentExpression ("," @AssignmentExpression)*)

AddIndex
	= "[" @Expression "]" #Index

PrimaryExpression
	= Constant
	/ CString
	/ "(" Expression ")"
	/ Name
	
//Literal
//=======

Constant
	= CFloat
	/ CInteger
	/ CChar

CFloat
	= { ( DECIMAL_FLOAT / HEX_FLOAT )  #Float } FLOAT_SUFFIX? _

DECIMAL_FLOAT
	= FRACTION EXPONENT?
	/ DIGIT+ EXPONENT

FRACTION
	= DIGIT* '.' DIGIT+
	/ DIGIT+ '.'

EXPONENT
	= [eE] [+\-]? DIGIT+

HEX_FLOAT
	= HEX_PREFIX HEX_FRACTION BINARY_EXPONENT?
	/ HEX_PREFIX HEX+ BINARY_EXPONENT

HEX_PREFIX
	= '0' [xX]

HEX_FRACTION
	= HEX* '.' HEX+
	/ HEX+ '.'

BINARY_EXPONENT
	= [pP] [+\-]? DIGIT+

FLOAT_SUFFIX
	= [flFL]

CInteger
	= { ( DECIMAL / HEXICAL / OCTAL ) #Integer } INT_SUFFIX? _

DECIMAL
	= [1-9] DIGIT*

HEXICAL
	= HEX_PREFIX HEX+

OCTAL
	= '0' [0-7]*

INT_SUFFIX
	= [uU] LONG_SUFFIX?
	/ LONG_SUFFIX [uU]?

LONG_SUFFIX
	= 'll'
	/ 'LL'
	/ [lL]

CString
	=  'L'? ('"' { STRING_CONTENT* #String } '"' _)+

CChar
	= 'L'? "'" { CHAR_CONTENT* #Character } "'" _

STRING_CONTENT
	= ESCAPE
	/ ![\"\n\\] .


CHAR_CONTENT
	= ESCAPE
	/ !['\n\\] .

ESCAPE
	= SIMPLE_ESCAPE
	/ OCTAL_ESCAPE
	/ HEX_ESCAPE
	/ UCHAR

SIMPLE_ESCAPE
	= '\\' ['\"?\\abfnrtv]
	
OCTAL_ESCAPE
	= '\\' [0-7] [0-7]? [0-7]?
	
HEX_ESCAPE
	= '\\x' HEX+
